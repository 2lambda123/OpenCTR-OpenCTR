#.rst:
# OpenCTR-Config
# --------------
#
# :command:`find_package` module for OpenCTR.

####################################################################
# OpenCTR - A free and open-source SDK for Nintendo 3DS homebrew. 
# 
# Copyright (C) 2015 The OpenCTR Project. 
# 
# This file is part of OpenCTR. 
# 
# OpenCTR is free software: you can redistribute it and/or modify 
# it under the terms of the GNU General Public License version 3 as 
# published by the Free Software Foundation.
# 
# OpenCTR is distributed in the hope that it will be useful, 
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License 
# along with OpenCTR. If not, see <http://www.gnu.org/licenses/>.
####################################################################

cmake_minimum_required(VERSION 3.0.0)
if(CMAKE_VERSION VERSION_LESS 3.0.0)
    set(OpenCTR_FOUND FALSE)
    set(OpenCTR_NOTFOUND_MESSAGE "OpenCTR requires CMake 3.0.0 or later.")
    return()
endif()

include(CMakeFindDependencyMacro)

set(OPENCTR_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

include(${CMAKE_CURRENT_LIST_DIR}/openctr-macros.cmake)

set(_supported_components artool 3dsxtool libctru libctr)

list(LENGTH OpenCTR_FIND_COMPONENTS len)
if((len EQUAL 0) OR (NOT OpenCTR_FIND_COMPONENTS))
    # TODO: Copy _supported_components into _components.
    set(_components artool 3dsxtool libctru)
else()
    set(_components ${OpenCTR_FIND_COMPONENTS})
endif()

foreach(_comp ${_components})
    # Make sure the component is a recognized component of OpenCTR.
    list(FIND _supported_components ${_comp} IDX)
    if(IDX EQUAL -1)
        set(OpenCTR_FOUND False)
        set(OpenCTR_NOTFOUND_MESSAGE "Unsupported component: ${_comp}")
        break()
    endif()

    # Find the component
    find_dependency(${_comp})

    # Check if the component was found.
    string(TOUPPER "${_comp}" COMP_NAME)
    if(${${COMP_NAME}_FOUND})
        set(OpenCTR_${_comp}_FOUND TRUE)
    endif()

endforeach()

check_required_components(OpenCTR)
